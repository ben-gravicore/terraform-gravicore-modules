# ----------------------------------------------------------------------------------------------------------------------
# VARIABLES / LOCALS / REMOTE STATE
# ----------------------------------------------------------------------------------------------------------------------

variable "external_id" {
  description = "https://docs.aws.amazon.com/IAM/latest/UserGuide/id_roles_create_for-user_externalid.html"
}

data "aws_iam_policy_document" "integration_assume_role" {
  count = "${var.create == "true" ? 1 : 0}"

  statement {
    actions = ["sts:AssumeRole"]

    principals {
      type        = "AWS"
      identifiers = ["arn:aws:iam::464622532012:root"]
    }

    condition {
      test     = "StringEquals"
      variable = "sts:ExternalId"

      values = [
        "${var.external_id}",
      ]
    }
  }
}

data "aws_iam_policy_document" "integration" {
  count = "${var.create == "true" ? 1 : 0}"

  statement {
    actions = [
      "apigateway:GET",
      "autoscaling:Describe*",
      "budgets:ViewBudget",
      "cloudfront:GetDistributionConfig",
      "cloudfront:ListDistributions",
      "cloudtrail:DescribeTrails",
      "cloudtrail:GetTrailStatus",
      "cloudwatch:Describe*",
      "cloudwatch:Get*",
      "cloudwatch:List*",
      "codedeploy:List*",
      "codedeploy:BatchGet*",
      "directconnect:Describe*",
      "dynamodb:List*",
      "dynamodb:Describe*",
      "ec2:Describe*",
      "ecs:Describe*",
      "ecs:List*",
      "elasticache:Describe*",
      "elasticache:List*",
      "elasticfilesystem:DescribeFileSystems",
      "elasticfilesystem:DescribeTags",
      "elasticloadbalancing:Describe*",
      "elasticmapreduce:List*",
      "elasticmapreduce:Describe*",
      "es:ListTags",
      "es:ListDomainNames",
      "es:DescribeElasticsearchDomains",
      "health:DescribeEvents",
      "health:DescribeEventDetails",
      "health:DescribeAffectedEntities",
      "kinesis:List*",
      "kinesis:Describe*",
      "lambda:AddPermission",
      "lambda:GetPolicy",
      "lambda:List*",
      "lambda:RemovePermission",
      "logs:Get*",
      "logs:Describe*",
      "logs:FilterLogEvents",
      "logs:TestMetricFilter",
      "logs:PutSubscriptionFilter",
      "logs:DeleteSubscriptionFilter",
      "logs:DescribeSubscriptionFilters",
      "rds:Describe*",
      "rds:List*",
      "redshift:DescribeClusters",
      "redshift:DescribeLoggingStatus",
      "route53:List*",
      "s3:GetBucketLogging",
      "s3:GetBucketLocation",
      "s3:GetBucketNotification",
      "s3:GetBucketTagging",
      "s3:ListAllMyBuckets",
      "s3:PutBucketNotification",
      "ses:Get*",
      "sns:List*",
      "sns:Publish",
      "sqs:ListQueues",
      "support:*",
      "tag:GetResources",
      "tag:GetTagKeys",
      "tag:GetTagValues",
      "xray:BatchGetTraces",
      "xray:GetTraceSummaries",
    ]

    resources = ["*"]
  }
}

# ----------------------------------------------------------------------------------------------------------------------
# MODULES / RESOURCES
# ----------------------------------------------------------------------------------------------------------------------

resource "aws_iam_policy" "integration" {
  count       = "${var.create == "true" ? 1 : 0}"
  name        = "${local.module_prefix}"
  description = "${var.desc_prefix} Policy for Datadog AWS Integration"

  policy = "${data.aws_iam_policy_document.integration.json}"
}

resource "aws_iam_role" "integration" {
  count       = "${var.create == "true" ? 1 : 0}"
  name        = "${local.module_prefix}"
  description = "${var.desc_prefix} Role for Datadog AWS Integration"
  tags        = "${local.tags}"

  assume_role_policy = "${data.aws_iam_policy_document.integration_assume_role.json}"
}

resource "aws_iam_role_policy_attachment" "integration" {
  count = "${var.create == "true" ? 1 : 0}"

  role       = "${join("", aws_iam_role.integration.*.name)}"
  policy_arn = "${join("", aws_iam_policy.integration.*.arn)}"
}

# ----------------------------------------------------------------------------------------------------------------------
# OUTPUTS
# ----------------------------------------------------------------------------------------------------------------------

output "account_id" {
  value = "${var.account_id}"
}

output "datadog_api_key" {
  value = "${var.datadog_api_key}"
}

output "integration_role_name" {
  value = "${join("", aws_iam_role.integration.*.name)}"
}

output "integration_role_arn" {
  value = "${join("", aws_iam_role.integration.*.arn)}"
}
